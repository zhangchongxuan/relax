apply plugin: 'com.android.application'

android {
    signingConfigs {
        mConfig {
            keyAlias 'map.key'
            keyPassword 'zhangcx0123.'
            storeFile file('/Volumes/移动硬盘/个人资料/mysign.keystore')
            storePassword 'zhangcx523.'
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.zhangcx"//部分包名
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 2
        versionName "1.2"



        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.mConfig
            applicationIdSuffix "relax"//包名后缀+relax     com.zhangcx.relax

            //是否输出日志
            buildConfigField 'boolean', 'IS_DEBUG', 'false'
            //对应服务器地址
            buildConfigField 'String', 'SERVER_URL0', '"http://www.release0.com"'
            buildConfigField 'String', 'SERVER_URL1', '"http://www.release1.com"'


        }
        debug {
            signingConfig signingConfigs.mConfig
            applicationIdSuffix "relaxdebug"//包名后缀+debug    com.zhangcx.relaxdebug

            //是否输出日志
            buildConfigField 'boolean', 'IS_DEBUG', 'true'
            //对应服务器地址
            buildConfigField 'String', 'SERVER_URL0', '"http://www.debug0.com"'
            buildConfigField 'String', 'SERVER_URL1', '"http://www.debug1.com"'

        }
    }

    //代码差异化配置,比如说测试生产不同的鉴权文件代码,颜色显示不同渠道等(以资源风格为例)

    //3.0之后必须加 flavorDimensions
    flavorDimensions "res"

    productFlavors {
        free {
            dimension "res"
            applicationIdSuffix "free"
            buildConfigField 'String', 'which', '"免费的"'
        }
        paid {
            dimension "res"
            applicationIdSuffix "paid"
            buildConfigField 'String', 'which', '"收费的"'

        }
    }

    sourceSets {
        free {
            res.srcDirs = ['flavors/free/res']
        }
        paid {
            res.srcDirs = ['flavors/paid/res']

        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile 'com.orhanobut:logger:2.1.1'
}
